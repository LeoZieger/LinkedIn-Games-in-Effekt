module src/util/vec2i


import test


type Vec2i = Tuple2[Int, Int]

def x(v: Vec2i): Int / {} = {v.first}
def y(v: Vec2i): Int / {} = {v.second}

def infixAdd(a: Vec2i, b: Vec2i): Vec2i / {} = {
  (a.x + b.x, a.y + b.y)
}

def infixSub(a: Vec2i, b: Vec2i): Vec2i / {} = {
  (a.x - b.x, a.y - b.y)
}

def equals(a: Vec2i, b: Vec2i): Bool / {} = {
  a.first == b.first && a.second == b.second
}

def infixEq(a: Vec2i, b: Vec2i): Bool / {} = {
  equals(a, b)
}

def infixNeq(a: Vec2i, b: Vec2i): Bool / {} = {
  not(a == b)
}

def mul(v: Vec2i, k: Int): Vec2i / {} = {
  (v.x * k, v.y * k)
}

def infixMul(v: Vec2i, k: Int): Vec2i / {} = {
  v.mul(k)
}

def length(v: Vec2i): Double / {} = {
  sqrt(v.x.toDouble.pow(2) + v.y.toDouble.pow(2))
}

def compare(a: Vec2i, b: Vec2i): Ordering / {} = {
  compareInt(a.x, b.x) match {
    case Less() => Less()
    case Greater() => Greater()
    case Equal() => compareInt(a.y, b.y)
  }
}

def show(v: Vec2i): String / {} = "(" ++ show(v.x) ++ "," ++ show(v.y) ++ ")"

def testSuite(): Bool = suite("util/vec2i") {
  test("x") {
    assertEqual((1,2).x, 1)
  }

  test("y") {
    assertEqual((1,2).y, 2)
  }

  test("a+b") {
    assertEqual((1,2) + (3,2), (4, 4))
  }

  test("a-b") {
    assertEqual((4,7) - (2,4), (2, 3))
  }

  test("a*k") {
    assertEqual((1,2) * 3, (3, 6))
  }

  test("length") {
    assertEqual((4, 3).length, 5.0)
  }

  test("length2") {
    assertEqual((0,1).length, 1.0)
  }

  test("equal") {
    assertTrue((1,2) == (1,2))
  }

  test("notEqual") {
    assertTrue((1,2) != (2,1))
  }

  test("compare1") {
    assertEqual(compare((1,2), (2,2)),
                Less())
  }

  test("compare2") {
    assertEqual(compare((3,2), (2,2)),
                Greater())
  }

  test("compare3") {
    assertEqual(compare((2,2), (2,3)),
                Less())
  }

  test("show") {
    assertEqual(show((1,2)), "(1,2)")
  }
}
